apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.zxm.easymqttclient"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName "1.0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled false
    }

    signingConfigs {
        def filePath = '../sign/keystore.jks'
        def pwd = '20190101'
        def alias = 'com.coding.zxm'

        release {
            storeFile file(filePath)
            storePassword pwd
            keyAlias alias
            keyPassword pwd
        }
        debug {
            keyAlias alias
            keyPassword pwd
            storeFile file(filePath)
            storePassword pwd
        }
    }

    buildTypes {
        release {
            //缩减代码
            minifyEnabled true
            //压缩优化
            zipAlignEnabled true
            //资源压缩
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //当前模式
            buildConfigField 'boolean', 'IS_DEBUG', 'false'

            signingConfig signingConfigs.release
        }

        debug {
            //当前模式
            buildConfigField 'boolean', 'IS_DEBUG', 'false'

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "mqtt"
    productFlavors {
        //开发
        dev {
            applicationIdSuffix = '.dev'
            versionNameSuffix "-debug"
            manifestPlaceholders = [APP_NAME: "@string/app_name_dev"]
            dimension "mqtt"
        }
        //发布
        publish {
            manifestPlaceholders = [APP_NAME: "@string/app_name"]
            dimension "mqtt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "EasyMqttClient_${variant.versionName}.${variant.versionCode}" +
                    "_${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["design"]
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]

    //引用Mqtt框架
    implementation project(':mqtt_master')

    //使用AAR
//    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$rootProject.mqttClientVersion"
//    implementation(name: 'mqtt_service_1.0.3_release', ext: 'aar')

    implementation rootProject.ext.dependencies["BaseRecyclerViewAdapterHelper"]

    implementation rootProject.ext.dependencies["utils"]

    //腾讯自更新&Buggly
    implementation rootProject.ext.dependencies["crashreport_upgrade"]

    //glide
    api rootProject.ext.dependencies["glide"]
    annotationProcessor rootProject.ext.dependencies["glide_compiler"]

}
